plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.71"
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.etesync"
version = "1.0.4"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    jcenter()
}

dependencies {
    def kotlin_version= "1.2.71"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def spongyCastleVersion = "1.54.0.0"
    implementation "com.madgag.spongycastle:core:$spongyCastleVersion"
    implementation "com.madgag.spongycastle:prov:$spongyCastleVersion"

    def okhttp3Version = "3.12.1"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3Version"

    implementation "com.google.code.gson:gson:1.7.2"
    implementation "org.apache.commons:commons-collections4:4.1"
    implementation "org.apache.commons:commons-lang3:3.8.1"
    implementation "commons-codec:commons-codec:1.7"

    testImplementation "junit:junit:4.12"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3Version"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.shouldSign = isReleaseVersion
if (shouldSign) {
    signing {
        useGpgCmd()
        sign configurations.archives
    }
}

uploadArchives {
  repositories {
    mavenDeployer {
      if (shouldSign) {
          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }
      pom.project {
          name "EteSync JVM"
          packaging "jar"
          description "EteSync API client for the JVM"
          url "https://www.etesync.com"

          scm {
              connection "scm:git:git://github.com/etesync/etesync-jvm.git"
              developerConnection "scm:git:ssh://github.com/etesync/etesync-jvm"
              url "https://github.com/etesync/etesync-jvm"
          }

          licenses {
              license {
                  name "LGPL-3.0-only"
                  url "https://spdx.org/licenses/LGPL-3.0-only.html"
              }
          }

          developers {
              developer {
                  id "tasn"
                  name "Tom Hacohen"
                  email "maven@stosb.com"
              }
          }
      }
    }
  }
}
